buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.apache.ivy:ivy:2.3.0'
	}
}

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.3.70'
	id 'java-library'
	id 'maven-publish'
	id "de.undercouch.download" version "4.0.4"
}

version = '1.0.0'
group = 'com.ford.pe.api.onboarding.build.cache'

ext {
	buildCacheArchiveName = "${project.name}-build-cache.zip"
	buildCacheDir = 'build-cache'
	nexusBuildCache = 'http://localhost:8081/repository/build-cache'
}

repositories {
	jcenter()
	maven {
		url nexusBuildCache
		metadataSources {
			artifact()
		}
	}
}

configurations {
	buildcache
}

dependencies {
	implementation platform('org.jetbrains.kotlin:kotlin-bom')
	implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
	testImplementation 'org.jetbrains.kotlin:kotlin-test'
	testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
	buildcache 'com.ford.pe.api.onboarding.build.cache:test-project:latest'
}

task archiveBuildCache(type: Zip) {
	archiveFileName = "$buildCacheArchiveName"
	destinationDirectory = file("$rootDir/$buildCacheDir")
	from('.gradle') {
		into '.gradle'
	}

	from(project.buildDir) {
		into 'build'
	}
}

task fetchRemoteBuildCache(type: Copy) {
	from configurations.buildcache {
		include '*.zip'
	}
	into buildCacheDir
}

task extractBuildCacheArchive(type: Copy) {
	from zipTree("$buildCacheDir/$buildCacheArchiveName")
	into project.rootDir
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact archiveBuildCache
		}
	}
	repositories {
		maven {
			url = nexusBuildCache
			credentials(PasswordCredentials) {
				username 'admin'
				password '9e20739f-193a-42a6-8d61-7146dcc20cd6'
			}
		}
	}
}
